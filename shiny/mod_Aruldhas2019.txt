$PROB
Aruldhas (2019) PopPK model

$PARAM TVCL=8.11, TVVd=44.7, MTT=0.932, NN=9, BW=19.5, 
$CMT DEPOT CENT

$GLOBAL 
int NDOSE = 0;
double dosetime[450];
double dose[450];

$MAIN
if(NEWIND < 2) NDOSE = 0; 

if(self.amt > 0 && self.cmt==1) {
 NDOSE = NDOSE + 1; 
 dosetime[NDOSE] = self.time;
 dose[NDOSE] = self.amt;
}

F_DEPOT = 0;
double KTR = (NN + 1) / MTT;
double NFAC = exp(lgamma(NN + 1));
double KINPT = pow(KTR, (NN + 1)) / NFAC;

double CL = TVCL * pow((BW / 19.5), 0.75) * exp(ETA1);
double VC = TVVd * (BW / 19.5) * exp(ETA2);
double BIO = 1 * exp(ETA3);

$ODE
double INPT = 0;
int i = 0;
while(i <= NDOSE) {
  double IPT = 0;
  if(SOLVERTIME >= dosetime[i]) {
    double delta = SOLVERTIME - dosetime[i];
    IPT = dose[i] * pow(delta, NN) * exp(-KTR * delta);  
  }
  INPT = INPT + IPT;
  ++i;
  }
dxdt_DEPOT = KINPT * INPT - KTR * DEPOT;
dxdt_CENT = KTR * DEPOT - (CL / VC) * CENT;

$OMEGA @annotated
ETA1: 0.42 : ETA on CL
ETA2: 0.68 : ETA on VC
ETA3: 0.522 : ETA on BIO

$TABLE
double CP = CENT / VC;

$CAPTURE CP CL VC BIO MTT ETA1 ETA2 ETA3
